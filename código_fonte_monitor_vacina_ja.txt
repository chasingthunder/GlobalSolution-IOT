#include <WiFi.h>
#include <PubSubClient.h>
#include "DHT.h"

#define DHTPIN 26
#define DHTTYPE DHT22
DHT dht(DHTPIN, DHTTYPE);

#define wifi_ssid "Wokwi-GUEST"
#define wifi_password ""
#define mqtt_server "test.mosquitto.org"

#define temperature_topic "sensor_temperatura"
#define humidity_topic "sensor_umidade"

WiFiClient espClient;
PubSubClient client(espClient);

void setup() {
  Serial.begin(9600);
  dht.begin();
  setup_wifi();
  client.setServer(mqtt_server, 1883);
  pinMode(14, OUTPUT);  // Green LED
  pinMode(12, OUTPUT);  // Red LED
}

void setup_wifi() {
  delay(10);
  Serial.println();
  Serial.print("Connecting to ");
  Serial.println(wifi_ssid);

  WiFi.begin(wifi_ssid, wifi_password);

  int attempts = 0;
  while (WiFi.status() != WL_CONNECTED && attempts < 40) {
    delay(500);
    Serial.print(".");
    attempts++;
  }

  if (WiFi.status() != WL_CONNECTED) {
    Serial.println("\nFailed to connect to WiFi. Please check your credentials.");
    while (1) {
      delay(1000);
    }
  }

  Serial.println("");
  Serial.println("WiFi connected");
  Serial.println("IP address: ");
  Serial.println(WiFi.localIP());
}

void loop() {
  delay(2000);

  float h = dht.readHumidity();
  float t = dht.readTemperature();

  if (isnan(h) || isnan(t)) {
    Serial.println("Failed to read from DHT sensor!");
    return;
  }

  Serial.print("Humidity: ");
  Serial.print(h);
  Serial.print(" %\t");
  Serial.print("Temperature: ");
  Serial.print(t);
  Serial.println(" *C");

  if ((t >= 2 && t <= 8) && h < 50) {
    digitalWrite(14, HIGH);  // Turn on the green LED
    digitalWrite(12, LOW);   // Turn off the red LED
  } else {
    digitalWrite(12, HIGH);  // Turn on the red LED
    digitalWrite(14, LOW);   // Turn off the green LED
  }

  Serial.println("---");
  delay(1000);

  client.connect("ESP32Client");
  client.publish(temperature_topic, String(t).c_str(), true);
  client.publish(humidity_topic, String(h).c_str(), true);
  client.disconnect();
}
